apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.airmap.airmapsdk'
def libVersion = '2.0.0-beta.5'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'
    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 27
        versionCode 1
        versionName "$libVersion"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [auth0Domain: "@string/com_auth0_domain", auth0Scheme: "@string/com_auth0_scheme"]
    }

    buildTypes {
        release {
            minifyEnabled false
            crunchPngs true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    aaptOptions {
        additionalParameters '--utf16' // Allows emoji in XML
    }
}

dependencies {
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.10.0"
    testImplementation "org.robolectric:robolectric:3.4.2"

    // support
    def supportLibraryVersion = '27.0.2'
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:support-annotations:$supportLibraryVersion"
    compile "com.android.support:support-fragment:$supportLibraryVersion"
    compile "com.android.support:support-v13:$supportLibraryVersion"

    // play services
    def playServicesVersion = '11.6.2'
    implementation "com.google.android.gms:play-services-location:$playServicesVersion"

    // mapbox
    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:5.3.1'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-locationlayer:0.2.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-services:2.2.9'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-ui:2.2.9'

    // shared libraries
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.8.2'
    implementation 'online.devliving:securedpreferencestore:0.3.1' // Newer version available, but updating requires making some api changes within our code
    implementation 'com.auth0.android:lock:2.8.0'

    // other
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.jakewharton.timber:timber:4.6.0'
    implementation 'com.github.bumptech.glide:glide:3.8.0' //Using Glide instead of Picasso because it is more efficient for the app's tutorial images
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.5.0@aar'
    implementation 'com.nakama.arraypageradapter:arraypageradapter:0.1.1'
    implementation 'org.bitbucket.b_c:jose4j:0.6.1'
    implementation 'com.google.protobuf:protobuf-java:3.5.0'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.0'
    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'
}

configurations {
    // to avoid double inclusion of support libraries
    all*.exclude group: 'com.android.support', module: 'support-v4'
}

def siteUrl = 'http://www.airmap.com'
def projectName = 'com.airmap.airmapsdk'
def gitUrl = 'https://github.com/airmap/AirMapSDK-Android.git'

bintray {
    user = bintray_user
    key = bintray_api_key
    configurations = ['archives']
    pkg {
        repo = 'maven'
        userOrg = 'airmapio'
        version libVersion
        name = projectName
        desc = 'Create Flights, Send Telemetry Data, Get Realtime Traffic Alerts.'
        licenses = ['MIT']
        labels = ['android', 'airmap', 'drone', 'airspace', 'sdk', 'aar']
        vcsUrl = gitUrl
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            name projectName
            url siteUrl
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://github.com/airmap/AirMapSDK-Android/blob/master/LICENSE'
                }
            }
            developers {
                developer {
                    id 'vanshg'
                    name 'Vansh Gandhi'
                    email 'vansh@airmap.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl

            }
        }.writeTo("$buildDir/pom.xml")
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
