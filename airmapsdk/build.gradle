apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.airmap.airmapsdk'
def libVersion = '2.0.1'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 27
        versionCode 1
        versionName "$libVersion"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [auth0Domain: "@string/com_auth0_domain", auth0Scheme: "@string/com_auth0_scheme"]

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            crunchPngs true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:2.10.0"
    testCompile "org.robolectric:robolectric:3.4.2"

    // support
    def supportLibraryVersion = '27.1.1'
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:support-annotations:$supportLibraryVersion"
    compile "com.android.support:support-fragment:$supportLibraryVersion"
    compile "com.android.support:support-v13:$supportLibraryVersion"

    // play services
    def playServicesVersion = '15.0.1'
    compile "com.google.android.gms:play-services-location:$playServicesVersion"

    // mapbox
    compile 'com.mapbox.mapboxsdk:mapbox-android-sdk:5.5.3'
    compile 'com.mapbox.mapboxsdk:mapbox-android-plugin-locationlayer:0.4.0'
    compile 'com.mapbox.mapboxsdk:mapbox-android-services:2.2.10'
    compile 'com.mapbox.mapboxsdk:mapbox-android-ui:2.2.10'

    // shared libraries
    compile 'joda-time:joda-time:2.9.9'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.googlecode.libphonenumber:libphonenumber:8.8.2'
    compile 'online.devliving:securedpreferencestore:0.3.1' // Newer version available, but updating requires making some api changes within our code
    compile 'com.auth0.android:lock:2.8.0'
    compile 'com.auth0.android:auth0:1.12.1'

    // other
    compile 'com.squareup.okhttp3:okhttp:3.11.0'
    compile 'com.jakewharton.timber:timber:4.6.0'
    compile 'org.bitbucket.b_c:jose4j:0.6.1'
    compile 'com.google.protobuf:protobuf-java:3.5.0'
    compile 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.0'
    compile 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'
}

configurations {
    // to avoid double inclusion of support libraries
    all*.exclude group: 'com.android.support', module: 'support-v4'
}

def siteUrl = 'http://www.airmap.com'
def projectName = 'com.airmap.airmapsdk'
def gitUrl = 'https://github.com/airmap/AirMapSDK-Android.git'

bintray {
    user = project.hasProperty('bintray_user') ? project.property('bintray_user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray_api_key') ? project.property('bintray_api_key') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        userOrg = 'airmapio'
        version libVersion
        name = projectName
        desc = 'Create Flights, Send Telemetry Data, Get Realtime Traffic Alerts.'
        licenses = ['MIT']
        labels = ['android', 'airmap', 'drone', 'airspace', 'sdk', 'aar']
        vcsUrl = gitUrl
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            name projectName
            url siteUrl
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://github.com/airmap/AirMapSDK-Android/blob/master/LICENSE'
                }
            }
            developers {
                developer {
                    id 'collin'
                    name 'Collin Vance'
                    email 'collin@airmap.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl

            }
        }.writeTo("$buildDir/pom.xml")
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
